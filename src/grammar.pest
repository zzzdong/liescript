program = { SOI ~ "\n"* ~ (stmt)* ~ stmt? ~ "\n"* ~ EOI }

stmt = { itemStmt | letStmt ~ ";" | exprStmt ~ ";" }

letStmt = { "let" ~ ident ~ "=" ~ exprStmt }

itemStmt = { structDef | funcDef | constItem }

structDef = { "struct" ~ ident ~ "{" ~ fieldItem* ~ "}" }

fieldItem = { ident ~ ":" ~ typeItem ~ "," }

funcDef = { "fn" ~ ident ~ "(" ~ argList ~ ")" ~ ("->" ~ typeItem )? ~ "{" ~ "}" }

argList = { argItem? ~ ("," ~ argItem)* }

argItem = { ident ~ ":" ~ typeItem }

constItem = { "const" ~ ident ~ "=" ~ literalExpr }

exprStmt = { literalExpr | sumExpr }

sumExpr = { 
    termExpr ~ "+" ~ sumExpr 
    | termExpr ~ "-" ~ sumExpr 
    | termExpr
}

termExpr = { 
    factorExpr ~ "*" ~ termExpr 
    | factorExpr ~ "/" ~ termExpr 
    | factorExpr ~ "%" ~ termExpr 
    | factorExpr
}

factorExpr = {
    prefixOp ~ await_primary
    | await_primary
}

prefixOp = { "+" | "-" | "!"}

await_primary = {
    "await" ~ primary
    | primary
}

primary = {
    ident ~ "." ~ ident
    | atom
}

atom = {
    ident
    | literalExpr
}


literalExpr = { boolLit | charLit | stringLit | floatLit | integerLit }

boolLit = @{ "true" | "false" }

charLit = @{ "'" ~ char ~ "'" }

integerLit = @{ "_"? ~ ASCII_DIGIT+ }

floatLit = @{ "_"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* }

stringLit = @{ "\"" ~ stringInner ~ "\"" }

stringInner = @{ char* }

char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }



typeItem = { builtinType | ident }

builtinType = { "bool" | "byte" | "char" | "int" | "float" | "string" }


WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }
